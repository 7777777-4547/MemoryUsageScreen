plugins {
    id 'fabric-loom' version '0.8-SNAPSHOT'
    id 'maven-publish'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id "com.modrinth.minotaur" version "1.1.0"
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = "${project.archives_base_name}-${project.minecraft_version}"
version = project.mod_version
group = project.maven_group
minecraft {
    accessWidener = file("src/main/resources/memoryusagescreen.accesswidener")
}
repositories {
	mavenLocal()
    maven { url "https://maven.shedaniel.me/" }
    maven { url "https://maven.terraformersmc.com/releases/" }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings minecraft.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modApi include("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    modApi "com.terraformersmc:modmenu:${project.modmenu_version}"

	modApi "de.klotzi111:KTIG:1+"
	
	//Bundling
	include "de.klotzi111:KTIG:1+"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {

    it.options.encoding = "UTF-8"

    it.options.release = 16
}

java {

    withSourcesJar()
}
jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}
publishing {
    publications {
        mavenJava(MavenPublication) {

            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    repositories {

    }
}
if (System.getenv('curesforgeapikey') != null && "${curesfg_id}" != "0") {
    curseforge {
        apiKey = System.getenv('curesforgeapikey')
        project {
            id = "${curesfg_id}"
            changelog = ""
            releaseType = "release"
            addGameVersion "${minecraft_version}"
            mainArtifact(file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar")) {
                displayName = "${archives_base_name}-${minecraft_version}-${mod_version}-Fabric"
                relations {
                    embeddedLibrary 'cloth-config'
                }
            }
        }
    }
}
if (System.getenv('modrinthapikey') != null && "${modrinth_id}" != "0") {
    task publishModrinth(type: com.modrinth.minotaur.TaskModrinthUpload) {

        token = System.getenv('modrinthapikey')
        projectId = "${modrinth_id}"
        versionName = "${archives_base_name}-${minecraft_version}-${mod_version}-Fabric"
        versionNumber = "${minecraft_version}-${mod_version}-Fabric"
        uploadFile = file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar")
        addGameVersion("${minecraft_version}")
        addLoader('fabric')
    }
}